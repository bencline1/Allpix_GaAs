# Define module and return the generated name as MODULE_NAME
ALLPIX_UNIQUE_MODULE(MODULE_NAME)

SET(BICHSEL_DATA_SUFFIX ".TAB" CACHE INTERNAL "Bichsel data files suffix" FORCE)

# Prefer local data files if not installed to other location
GET_FILENAME_COMPONENT(ABSOLUTE_INSTALL_PREFIX ${CMAKE_INSTALL_PREFIX} ABSOLUTE)
IF(ABSOLUTE_INSTALL_PREFIX STREQUAL PROJECT_SOURCE_DIR)
    # Use local data directory (unless other is given)
    SET(BICHSEL_DATA_DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}" CACHE PATH "Bichsel data directory" FORCE)
ELSE()
    # Set the install path to share
    SET(BICHSEL_DATA_DIRECTORY "share/${CMAKE_PROJECT_NAME}/data" CACHE PATH "Bichsel data directory" FORCE)
ENDIF()

SET(DATA_INSTALL_DIRECTORY "${BICHSEL_DATA_DIRECTORY}")
IF(NOT IS_ABSOLUTE ${BICHSEL_DATA_DIRECTORY})
    SET(BICHSEL_DATA_DIRECTORY "${CMAKE_INSTALL_PREFIX}/${BICHSEL_DATA_DIRECTORY}" CACHE PATH "Bichsel data directory" FORCE)
ENDIF()

# Install the files to the data directory
# NOTE: With default install path this does not change anything
FILE(GLOB MODEL_FILES LIST_DIRECTORIES false CONFIGURE_DEPENDS "*.TAB")
INSTALL(FILES ${MODEL_FILES} DESTINATION ${DATA_INSTALL_DIRECTORY} COMPONENT data)

ADD_DEFINITIONS(-DALLPIX_BICHSEL_DATA_DIRECTORY="${BICHSEL_DATA_DIRECTORY}"  -DALLPIX_BICHSEL_DATA_SUFFIX="${BICHSEL_DATA_SUFFIX}")

# Add source files to library
ALLPIX_MODULE_SOURCES(${MODULE_NAME}
    DepositionBichselModule.cpp
    MazziottaIonizer.cpp
)

TARGET_LINK_LIBRARIES(${MODULE_NAME} ROOT::Graf3d)

# Provide standard install target
ALLPIX_MODULE_INSTALL(${MODULE_NAME})
